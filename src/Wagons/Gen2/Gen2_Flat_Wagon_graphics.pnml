/*
 *  Copyright (C) 2025 althonos
 *  Copyright (C) 2017 Transportman
 *  This file is part of the 2cc Trains In NML - Narrow Gauge edition
 *  This program is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation; either version 2 of the License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program; if not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
*/

#define GEN2_FLAT_LIVERY(NAME, L, OFFSET) \
	spriteset(spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S1, format_string("gfx/Wagons/Gen2/Gen2_Flat_Wagon_%s.png", #NAME)) { \
		template_2cc_wagons(OFFSET, 1) \
	} \
	spriteset(spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S2, format_string("gfx/Wagons/Gen2/Gen2_Flat_Wagon_%s.png", #NAME)) { \
		template_2cc_wagons(OFFSET, 32) \
	} \
	spriteset(spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S3, format_string("gfx/Wagons/Gen2/Gen2_Flat_Wagon_%s.png", #NAME)) { \
		template_2cc_wagons(OFFSET, 64) \
	} \
	spritegroup spritegroup_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L { \
		loading: [spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S1, spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S2, spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S3]; \
		loaded: [spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S1, spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S2, spriteset_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _ ## L ## _S3]; \
	}

#define GEN2_FLAT_WAGON(NAME) \
	GEN2_FLAT_LIVERY(NAME, L1, 1) \
	GEN2_FLAT_LIVERY(NAME, L2, 179) \
	GEN2_FLAT_LIVERY(NAME, L3, 357) \
	GEN2_FLAT_LIVERY(NAME, L4, 535) \
	random_switch(FEAT_TRAINS, SELF, switch_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _livery) { \
		7: spritegroup_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _L1; \
		1: spritegroup_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _L2; \
		1: spritegroup_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _L3; \
		1: spritegroup_wagon_Narrow_Gen2_Flat_Wagon_ ## NAME ## _L4; \
	}


GEN2_FLAT_WAGON(Crates)
GEN2_FLAT_WAGON(Planks)
GEN2_FLAT_WAGON(Steel)
GEN2_FLAT_WAGON(Machinery)
GEN2_FLAT_WAGON(Copper)
GEN2_FLAT_WAGON(Wood)
GEN2_FLAT_WAGON(Paper)

/*
* PURCHASE
*/

spriteset(spriteset_wagon_Narrow_Gen2_Flat_Wagon_purchase, "gfx/Wagons/Gen2/Gen2_Flat_Wagon_Purchase.png") {
	template_purchase_wagon(1, 1)
}


// Goods and Engineering Supplies have multiple liveries
random_switch(FEAT_TRAINS, SELF, switch_wagon_Narrow_Gen2_Flat_Wagon_Goods_livery) {
	7: switch_wagon_Narrow_Gen2_Flat_Wagon_Crates_livery;
	3: switch_wagon_Narrow_Gen2_Flat_Wagon_Machinery_livery;
}

// Building materials can be planks, steel or copper
random_switch(FEAT_TRAINS, SELF, switch_wagon_Narrow_Gen2_Flat_Wagon_Building_Materials_livery) {
	2: switch_wagon_Narrow_Gen2_Flat_Wagon_Planks_livery;
	2: switch_wagon_Narrow_Gen2_Flat_Wagon_Steel_livery;
	1: switch_wagon_Narrow_Gen2_Flat_Wagon_Copper_livery;
}

switch(FEAT_TRAINS, SELF, switch_wagon_Narrow_Gen2_Flat_Wagon_cargo_selection, cargo_type_in_veh) {
	GOOD: switch_wagon_Narrow_Gen2_Flat_Wagon_Goods_livery;
	WOOD: switch_wagon_Narrow_Gen2_Flat_Wagon_Wood_livery;
	STEL: switch_wagon_Narrow_Gen2_Flat_Wagon_Steel_livery;
	COPR: switch_wagon_Narrow_Gen2_Flat_Wagon_Copper_livery;
	BDMT: switch_wagon_Narrow_Gen2_Flat_Wagon_Building_Materials_livery;
	ENSP: switch_wagon_Narrow_Gen2_Flat_Wagon_Goods_livery;
	FMSP: switch_wagon_Narrow_Gen2_Flat_Wagon_Goods_livery;
	WDPR: switch_wagon_Narrow_Gen2_Flat_Wagon_Planks_livery;
	PAPR: switch_wagon_Narrow_Gen2_Flat_Wagon_Paper_livery;
	// YETI: switch_wagon_Narrow_Gen2_Flat_Wagon_YETI_livery;
	// YETY: switch_wagon_Narrow_Gen2_Flat_Wagon_YETI_livery;
	switch_wagon_Narrow_Gen2_Flat_Wagon_Crates_livery; 
}

//Add defined switches 

ENGINECAPACITYSWITCH(VEHIDCODE)

RUNNINGCOSTENGINESWITCH

PURCHASETEXTSWITCH